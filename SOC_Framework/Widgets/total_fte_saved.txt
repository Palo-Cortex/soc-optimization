Name=Total FTEs Saved
Description=How many FTEs time it would have taken
XQL=
dataset = xsiam_playbookmetrics_raw 
| alter Tasks = Tasks->[]
| arrayexpand Tasks

// TODO apply a datamodel to these two objects instead (Tasks and Alert)
| alter 
taskState = tasks->state,
taskType = tasks->type,
taskId = tasks->id,
taskName = tasks->name,
scriptID = tasks->scriptId,
incidentID = alert->parentXDRIncident,
alertID = alert->id,
alertName = alert->name,
alertType = alert->type,
playbookId = alert->playbookId

| filter taskType not in ("start", "title", "condition")
| filter playbookId != ""

// Since the job which posts this data runs every 15 min there may be duplicate data the more frequent it is run
| alter dedupkey = concat(incidentID,taskId)
| dedup dedupkey

// Filter for just automation
//| filter alerttype != "Unclassified"
| filter taskState = "Completed"
| filter tasktype = "regular"

//TODO make this a lookup table and join instead to make it scalable
| join type = inner (dataset = value_tags 
| fields Category as value_category, ScriptID as value_scriptid, `Tag` as value_tag, TaskName as value_taskname, Time as value_time, PlaybookID as playbook_id
) as vt (scriptID contains vt.value_scriptid)
| fields value_category, value_scriptid, value_tag, value_taskname, value_time, _time, playbook_id 
| filter ((value_time != null and value_time != """""")) 
| filter ((value_scriptid != null and value_scriptid != """""")) 

| alter soc_event_minutes = to_integer(value_time )
| comp sum(soc_event_minutes) as total_soc_minutes, first(_time) as TimeFrameBegin
| alter TimeFrameEnds = time_frame_end()
| alter TimeFrameWeeks = divide(timestamp_diff(TimeFrameEnds  , TimeFrameBegin  ,"DAY"),7)
| alter TimeFrameWeeks = if(TimeFrameWeeks < 1, 1, TimeFrameWeeks )
| alter TimeFrameHours = multiply(TimeFrameWeeks, 40)
| alter total_soc_hours = divide(total_soc_minutes,60)
| alter total_fte_saved = round(divide(total_soc_hours, TimeFrameHours))







| view graph type = single subtype = standard header = "Total FTEs Saved" yaxis = total_fte_saved dataunit = "FTEs" 
